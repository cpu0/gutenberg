10 C:#N=0
20 *MAIN
30 R: SET RANDOM PEN COLOR (1..3)
40 C:@B1363=?\3+1
50 R: SET RANDOM SQUARE LOCATION
60 C:#X=?\150-75
70 C:#Y=?\80-40
80 R: SET RANDOM SQUARE SIZE
90 C:#L=?\30+5
100 R: DRAW A FILLED IN SQUARE WITH BORDER
110 U:*SQUARE
120 PA:40
130 R: RANDOMIZE THE SCREEN COLORS
140 C:@B708=(((?\16)*16)+((?\8)*2))
150 C:@B709=(((?\16)*16)+((?\8)*2))
160 C:@B710=(((?\16)*16)+((?\8)*2))
170 C:#N=#N+1
180 J(#N<99):*MAIN
190 E:
200 R: ====================
210 *SQUARE
220 GR:TURNTO 90
230 C:#I=0
240 R: DRAW A FILLED IN SQUARE
250 *LOOP
260 GR:GOTO #X,#Y+#I;DRAW #L-1
270 C:#I=#I+1
280 J(#I<#L):*LOOP
290 R: DRAW A BLACK BORDER AROUND THE SQUARE
300 C:@B1363=0
310 GR:TURNTO 0
320 GR:GOTO #X,#Y; DRAW #L-1
330 GR:GOTO #X+#L-1,#Y; DRAW #L-1
340 GR:TURNTO 90
350 GR:GOTO #X,#Y; DRAW #L-1
360 GR:GOTO #X,#Y+#L-1; DRAW #L-1
370 E:
